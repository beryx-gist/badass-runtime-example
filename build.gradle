plugins {
    id 'org.beryx.runtime' version '1.12.5'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
}

application {
    mainClass = 'org.beryx.nonmodular.hello.Hello'
    mainModule = 'org.beryx.nonmodular.hello'
    applicationName = 'hello'
    applicationDefaultJvmArgs = ['-Dlogback.configurationFile=./logback.xml']
}

jar {
    manifest {
        attributes 'Implementation-Title': "non-modular-hello",
                'Main-Class': 'org.beryx.nonmodular.hello.Hello'
    }
}

runtime {
    modules = ['java.naming', 'java.xml']
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions = ['--win-console']
        }
    }
}


tasks.jpackage.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/jpackage/$project.name/app")
    }
}
